&Перед("ПриСозданииНаСервере")
// Переопределяемая процедура, вызываемая из одноименного обработчика события формы.
//
// Параметры:
// 	Форма - форма, из обработчика события которой происходит вызов процедуры.
//	см. справочную информацию по событиям управляемой формы.
//
Процедура ПередПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Форма.ИмяФормы="Документ.АвансовыйОтчет.Форма.ФормаДокумента" Тогда
		
		//Добавляем колонку Комментарий в табличную часть ПрочиеРасходы на форме
		
		РодительЭлемента = Форма.Элементы.ГруппаСтраницы.ПодчиненныеЭлементы.ГруппаРасходы.ПодчиненныеЭлементы.ПрочиеРасходы;
		Элемент = Форма.Элементы.Добавить("ПрочиеРасходыКомментарий", Тип("ПолеФормы"),РодительЭлемента);
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.Заголовок = "Содержание";
		Элемент.ПутьКДанным = "Объект.ПрочиеРасходы.Комментарий"; 
		
		//Добавляем табличную часть ВыданныеАвансы на форму
		
		РодительЭлемента_ГруппаСтраницы = Форма.Элементы.ГруппаСтраницы;
		
		ГруппаАвансы = Форма.Элементы.Добавить("ГруппаВыданныеАвансы", Тип("ГруппаФормы"),РодительЭлемента_ГруппаСтраницы);
		ГруппаАвансы.Вид = ВидГруппыФормы.Страница;
		ГруппаАвансы.Заголовок = "Выданные Авансы";
		ГруппаАвансы.ПутьКДаннымЗаголовка = "Объект.ВыданныеАвансы.КоличествоСтрок";
		
		ГруппаКолАвансы = Форма.Элементы.Добавить("ГруппаВыданныеАвансыАналитика", Тип("ТаблицаФормы"),ГруппаАвансы);
		ГруппаКолАвансы.ПутьКДанным = "Объект.ВыданныеАвансы";
		
		Элемент = Форма.Элементы.Добавить("ВыданныеАвансыДокументАванса", Тип("ПолеФормы"),ГруппаКолАвансы);
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.Заголовок = "Документ аванса";
		Элемент.ПутьКДанным = "Объект.ВыданныеАвансы.ДокументАванса";
		
	ИначеЕсли Форма.ИмяФормы="Документ.ПоступлениеБезналичныхДенежныхСредств.Форма.ФормаДокумента"
		или Форма.ИмяФормы="Документ.СписаниеБезналичныхДенежныхСредств.Форма.ФормаДокумента" Тогда
		
		РодительЭлемента = Форма.Элементы.ГруппаСтраницы.ПодчиненныеЭлементы.СтраницаОсновное.ПодчиненныеЭлементы.ГруппаДокумент.ПодчиненныеЭлементы.ГруппаДополнительныеРеквизитыШапки;
		Элемент = Форма.Элементы.Добавить("ДоговорКонтрагента", Тип("ПолеФормы"), РодительЭлемента);
		Элемент.Вид =				ВидПоляФормы.ПолеВвода;
		Элемент.Заголовок =		"Договор контрагента";
		Элемент.ПутьКДанным =	"Объект.Договор";
		
		Массив = Новый Массив(); 
		Массив.Добавить(Тип("СправочникСсылка.ДоговорыКонтрагентов")); 
		Элемент.ОграничениеТипа = Новый ОписаниеТипов(Массив);	
		
		Условие1 = Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты;
		Условие2 = Форма.Элементы.Договор.Видимость = Ложь;
		Условие3 = Форма.Элементы.СтраницаРасшифровка.Видимость = Ложь;
		Элемент.Видимость =  Условие1 и (Условие2 или Условие3);
		
	КонецЕсли;
	
КонецПроцедуры

